model{
  #  Regression priors
  #  Percent white at beginning of study
  alpha[1] ~ dunif(0, 100)
  alpha[2] ~ dunif(0, 100)
  alpha[3] ~ dunif(0, 100)
  snow_eff ~ dnorm(0, 0.001)
  elev_eff ~ dnorm(0, 0.001)
  
  #  "Process error"
  for(i in 1:3){
    tau[i] ~ dunif(0, 1000) 
    sigma[i] <- 1 / sqrt(tau[i]) 
  }
  
  #  Start date of molt
  start_pop ~ dunif(first_day, last_day) #uniformly distr.between first and last day of data
  
  #  End date of molt
  end_pop ~ dunif(start_pop, last_day)
  
  #  Error to describe variation among cameras in start and end dates
  tau_start ~ dunif(0, 300)
  sigma_start <- 1 / sqrt(tau_start)
  tau_end ~ dunif(0, 300)
  sigma_end <- 1 / sqrt(tau_end)
  
  #  Random effect to allow each camera to observe different molt dates
  for(i in 1:ncam){
    start_cam[i] ~ dnorm(start_pop, tau_start)T(first_day, last_day)
    end_cam[i] ~ dnorm(end_pop, tau_end)T(first_day, last_day)
  }
  
  #  Likelihood
  for(i in 1:nobs){
    J[i] <- 1 + (day[i] > start_cam[cam[i]]) + (day[i] > end_cam[cam[i]])
    
    #mu[i] <- alpha
    mu[i] <- alpha[J[i]] + snow_eff * snow[i] + elev_eff * elev[i]
    
    white[i] ~ dnorm(mu[i], tau[J[i]])T(0, 100)
  }
}
model{
  #  Regression priors
  #  Percent white at beginning of study
  alpha[1] ~ dunif(0, 100)
  alpha[2] ~ dunif(-1000, 1000)
  alpha[3] ~ dunif(0, 100)

  #  "Process error"
  for(i in 1:3){
    tau[i] ~ dunif(0, 1000) 
    sigma[i] <- 1 / sqrt(tau[i]) 
  }

  #  Slope to describe transition from start to end
  beta ~ dunif(-100, 100)
  
  #  Start date of molt at population scale
  start_pop ~ dunif(first_day, last_day)

  #  End date of molt at population scale
  end_pop ~ dunif(first_day, last_day)

  #  Error to describe variation among cameras in start and end dates AND 
  #   account for repeat observations
  tau_start ~ dunif(0, 300)
  sigma_start <- 1 / sqrt(tau_start)
  tau_end ~ dunif(0, 300)
  sigma_end <- 1 / sqrt(tau_end)

  #  Random effect to allow each camera to observe different molt dates
  for(i in 1:ncam){
    start_cam[i] ~ dnorm(start_pop, tau_start)T(first_day, last_day)
    end_cam[i] ~ dnorm(end_pop, tau_end)T(first_day, last_day)
  }

  #  Likelihood
  for(i in 1:nobs){
    J[i] <- 1 + (day[i] > start_cam[cam[i]]) + (day[i] > end_cam[cam[i]])

    mu[i] <- alpha[J[i]] +
      beta * (day[i] - start_cam[cam[i]]) * equals(J[i], 2)

    white[i] ~ dnorm(mu[i], tau[J[i]])T(0, 100)
  }
}